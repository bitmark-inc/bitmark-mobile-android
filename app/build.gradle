apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply from: 'versioning.gradle'

apply from: '../autodimension.gradle'

apply plugin: 'io.fabric'

def enableSeparateBuildPerCPUArchitecture = false

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    applicationId "com.bitmark.registry"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode buildVersionCode()
    versionName buildVersionName()
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    ndk {
      abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
    }
    externalNativeBuild {
      cmake {
        cppFlags ""
      }
    }

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation":
            "$projectDir/schemas".toString()]
      }
    }
  }

  splits {
    abi {
      reset()
      enable enableSeparateBuildPerCPUArchitecture
      universalApk false // If true, also generate a universal APK
      include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
    }
  }

  androidExtensions {
    experimental = true
  }

  signingConfigs {

    debug {
      def keystorePropertiesFile = rootProject.file(
          "keystores/debug.properties")
      def keystoreProperties = new Properties()
      keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
      keyAlias keystoreProperties['key.alias']
      keyPassword keystoreProperties['key.alias.password']
      storeFile file('../keystores/debug.keystore')
      storePassword keystoreProperties['key.store.password']
    }

    release {
      def keystorePropertiesFile = rootProject.file(
          "keystores/release.properties")
      def keystoreProperties = new Properties()
      keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
      keyAlias keystoreProperties['key.alias']
      keyPassword keystoreProperties['key.alias.password']
      storeFile file('../keystores/release.keystore')
      storePassword keystoreProperties['key.store.password']
    }
  }

  buildTypes {

    debug {
      debuggable true
      minifyEnabled false
      signingConfig signingConfigs.debug
    }

    release {
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile("proguard-android.txt"),
          "proguard-rules.pro"
    }
  }
  // applicationVariants are e.g. debug, release
  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      // For each separate APK per architecture, set a unique version code as described here:
      // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
      def versionCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3, "x86_64": 4]
      def abi = output.getFilter(com.android.build.OutputFile.ABI)
      if (abi != null) {
        // null for the universal-debug, universal-release variants
        output.versionCodeOverride =
            versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
      }
    }
  }

  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  flavorDimensions "version"

  productFlavors {

    inhouse {
      dimension "version"
      applicationIdSuffix ".inhouse"
      resValue "string", "app_name", "Bitmark"
      manifestPlaceholders = [
          appIcon: "@mipmap/ic_launcher_dev",
          appIconRound: "@mipmap/ic_launcher_dev_round"
      ]
      ext.betaDistributionReleaseNotesFilePath =
          "${project.rootDir}/distribution/release_note.txt"
      ext.betaDistributionEmailsFilePath =
          "${project.rootDir}/distribution/testers.txt"

      buildConfigField "String", "CORE_API_ENDPOINT",
          "\"https://api.test.bitmark.com\""
      buildConfigField "String", "MOBILE_SERVER_EMPOINT",
          "\"https://bm.test.bitmark.com\""
      buildConfigField "String", "FILE_COURIER_SERVER_ENPOINT",
          "\"https://file-courier.test.bitmark.com\""
      buildConfigField "String", "PROFILE_SERVER_ENDPOINT",
          "\"https://profiles.test.bitmark.com\""
      buildConfigField "String", "REGISTRY_WEBSITE",
          "\"https://registry.test.bitmark.com\""
      buildConfigField "String", "ZERO_ADDRESS",
          "\"dw9MQXcC5rJZb3QE1nz86PiQAheMP1dx9M3dr52tT8NNs14m33\""
      buildConfigField "String", "KEY_ACCOUNT_SERVER_ENDPOINT",
          "\"https://key.test.bitmarkaccountassets.com\""
      buildConfigField "String", "REGISTRY_WEB_ENDPOINT",
          "\"https://registry.test.bitmark.com\""
      buildConfigField "String", "NOTIFICATION_CLIENT",
          "\"registryinhouse\""
      buildConfigField "String", "TERMS_OF_SERVICE_URL",
          "\"https://bitmark.com/en/legal/terms?env=app\""
      buildConfigField "String", "PRIVACY_POLICY_URL",
          "\"https://bitmark.com/en/legal/privacy?env=app\""
    }

    prd {
      dimension "version"
      resValue "string", "app_name", "Bitmark"
      manifestPlaceholders = [
          appIcon: "@mipmap/ic_launcher",
          appIconRound: "@mipmap/ic_launcher_round"
      ]
      ext.betaDistributionReleaseNotesFilePath =
          "${project.rootDir}/distribution/release_note.txt"
      ext.betaDistributionEmailsFilePath =
          "${project.rootDir}/distribution/testers.txt"

      buildConfigField "String", "CORE_API_ENDPOINT",
          "\"https://api.bitmark.com\""
      buildConfigField "String", "MOBILE_SERVER_EMPOINT",
          "\"https://bm.bitmark.com\""
      buildConfigField "String", "FILE_COURIER_SERVER_ENPOINT",
          "\"https://file-courier.bitmark.com\""
      buildConfigField "String", "PROFILE_SERVER_ENDPOINT",
          "\"https://profiles.bitmark.com\""
      buildConfigField "String", "REGISTRY_WEBSITE",
          "\"https://registry.bitmark.com\""
      buildConfigField "String", "ZERO_ADDRESS",
          "\"a3ezwdYVEVrHwszQrYzDTCAZwUD3yKtNsCq9YhEu97bPaGAKy1\""
      buildConfigField "String", "KEY_ACCOUNT_SERVER_ENDPOINT",
          "\"https://key.bitmarkaccountassets.com\""
      buildConfigField "String", "REGISTRY_WEB_ENDPOINT",
          "\"https://registry.bitmark.com\""
      buildConfigField "String", "NOTIFICATION_CLIENT",
          "\"registry\""
      buildConfigField "String", "TERMS_OF_SERVICE_URL",
          "\"https://bitmark.com/en/legal/terms?env=app\""
      buildConfigField "String", "PRIVACY_POLICY_URL",
          "\"https://bitmark.com/en/legal/privacy?env=app\""
    }
  }

  applicationVariants.all { variant ->
    def name = variant.getName()

    if (name.contains("inhouse")) {
      variant.buildConfigField 'int', 'KEY_VALIDITY_DURATION', '120'
    } else if (name.contains("prd")) {
      variant.buildConfigField 'int', 'KEY_VALIDITY_DURATION', '600'
    }
  }
}

dependencies {

  // Basic
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  // temporarily use this version of appcompat to avoid crashing in IntercomMessengerActivity
  // @see https://community.intercom.com/t/intercom-android-sdk-crashing-with-android-appcompat-1-1-0-alpha03/1095
  implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'

  implementation 'androidx.core:core-ktx:1.2.0-alpha02'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

  // Dagger
  implementation 'com.google.dagger:dagger-android:2.23.2'
  implementation 'com.google.dagger:dagger-android-support:2.23.2'
  kapt 'com.google.dagger:dagger-android-processor:2.23.2'
  kapt 'com.google.dagger:dagger-compiler:2.23.2'

  // Rx2
  implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

  // Retrofit + Okhttp
  implementation "com.squareup.retrofit2:retrofit:2.6.0"
  implementation "com.squareup.retrofit2:adapter-rxjava2:2.6.0"
  implementation "com.squareup.retrofit2:converter-gson:2.6.0"
  implementation "com.squareup.okhttp3:okhttp:4.0.0"
  implementation "com.squareup.okhttp3:logging-interceptor:4.0.0"

  // Architecture component
  implementation "android.arch.lifecycle:livedata:2.0.0"
  implementation "android.arch.lifecycle:runtime:2.0.0"
  kapt "android.arch.lifecycle:compiler:2.0.0"

  // Room
  implementation 'androidx.room:room-runtime:2.1.0'
  kapt 'androidx.room:room-compiler:2.1.0'
  implementation 'androidx.room:room-rxjava2:2.1.0'

  // Others
  implementation 'com.bitmark.sdk:android-sdk:2.1.0-SNAPSHOT'
  implementation 'com.google.firebase:firebase-messaging:19.0.1'
  implementation 'com.aurelhubert:ahbottomnavigation:2.3.4'
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation 'com.google.zxing:core:3.4.0'
  implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
  implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
  implementation 'io.intercom.android:intercom-sdk:5.4.0'
  implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
    transitive = true
  }


  // Testing
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test:runner:1.3.0-alpha01'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha01'
}

task fillSecretKey << {
  def keyPropertiesFile = rootProject.file('key.properties')
  def keyProperties = new Properties()
  keyProperties.load(new FileInputStream(keyPropertiesFile))
  def apiKeyFile = file('src/main/jni/api-key.cpp')
  def content = apiKeyFile.getText()
  content = content.replace('bitmark-api-key-to-be-filled',
      keyProperties['api.key.bitmark'])
      .
      replace('intercom-api-key-to-be-filled',
          keyProperties['api.key.intercom'])
  apiKeyFile.bytes = []
  apiKeyFile.text = content
}

configurations.all {
  resolutionStrategy.eachDependency { details ->
    def requested = details.requested
    if (requested.group == 'androidx.appcompat') {
      details.useVersion '1.1.0-alpha02'
    }
  }
}

apply plugin: 'com.google.gms.google-services'
